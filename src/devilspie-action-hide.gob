%h{
#include <libwnck/window.h>
#include <libwnck/screen.h>

#include "tristate-dummy.h"
#include "devilspie-action.h"
#include "devilspie.h"
%}

class DevilsPie:Action:Hide from DevilsPie:Action {

  public DevilsPieTriState skip_pager;
  property ENUM skip_pager
  (nick = _("Skip Pager"),
   blurb = _("Window does not appear in the pager"),
   flags = CONSTRUCT,
   enum_type = DevilsPie:TriState, default_value = TRI_UNSET, link);

  public DevilsPieTriState skip_tasklist;
  property ENUM skip_tasklist
  (nick = _("Skip Task List"),
   blurb = _("Window does not appear in the task list"),
   flags = CONSTRUCT,
   enum_type = DevilsPie:TriState, default_value = TRI_UNSET, link);

  override (DevilsPie:Action) gboolean run(DevilsPie:Action *self (check null type), Wnck:Window *window (check null type)) {
    DevilsPieActionHide *a = (DevilsPieActionHide*)self;
    /* Assume that a true/false tristate in a boolean context is correct */
    if (a->skip_pager != TRI_UNSET) wnck_window_set_skip_pager(window, a->skip_pager);
    if (a->skip_tasklist != TRI_UNSET) wnck_window_set_skip_tasklist(window, a->skip_tasklist);
    return TRUE;
  }
  
  public GObject *new(void) {
    return (GObject*)GET_NEW;
  }
}
