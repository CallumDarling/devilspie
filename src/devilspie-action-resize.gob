%h{
#define WNCK_I_KNOW_THIS_IS_UNSTABLE
#include "config.h"
#include "libwnck/window.h"
#include "tristate-dummy.h"
#include "devilspie-action.h"
#include "devilspie.h"

/* Eww, the wnck headers don't declare this */
void wnck_window_set_fullscreen(WnckWindow *window, gboolean fullscreen);

%}

class DevilsPie:Action:Resize from DevilsPie:Action {

  public DevilsPieTriState maximized;
  property ENUM maximized
  (nick = _("Maximized"),
   blurb = _("Window is maximized on the screen"),
   flags = CONSTRUCT,
   enum_type = DevilsPie:TriState, default_value = TRI_UNSET, link);

public DevilsPieTriState maximized_horizontally;
  property ENUM maximized_horizontally
  (nick = _("Maximized Horizontally"),
   blurb = _("Window is maximized horizontally on the screen"),
   flags = CONSTRUCT,
   enum_type = DevilsPie:TriState, default_value = TRI_UNSET, link);

  public DevilsPieTriState maximized_vertically;
  property ENUM maximized_vertically
  (nick = _("Minimized Vertically"),
   blurb = _("Window is minimized vertically on the screen"),
   flags = CONSTRUCT,
   enum_type = DevilsPie:TriState, default_value = TRI_UNSET, link);

  public DevilsPieTriState minimized;
  property ENUM minimized
  (nick = _("Minimized"),
   blurb = _("Window is minimized"),
   flags = CONSTRUCT,
   enum_type = DevilsPie:TriState, default_value = TRI_UNSET, link);

  public DevilsPieTriState fullscreen;
  property ENUM fullscreen
  (nick = _("Full screen"),
   blurb = _("Window fills the screen"),
   flags = CONSTRUCT,
   enum_type = DevilsPie:TriState, default_value = TRI_UNSET, link);

  override (DevilsPie:Action) gboolean run(DevilsPie:Action *self (check null type), Wnck:Window *window (check null type)) {
    DevilsPieActionResize *a = (DevilsPieActionResize*)self;
    /* Assume that a true/false tristate in a boolean context is correct */
    /* Assume the user is not totally stupid and has not set both
    maximize and minimize */

    /* Handle maximized */
    if (a->maximized == TRI_TRUE)
      wnck_window_maximize(window);
    else if (a->maximized == TRI_FALSE)
      wnck_window_unmaximize(window);

    /* Handle maximized horizontally */
    if (a->maximized_horizontally == TRI_TRUE)
      wnck_window_maximize_horizontally(window);
    else if (a->maximized_horizontally == TRI_FALSE)
      wnck_window_unmaximize_horizontally(window);

    /* Handle maximized vertically*/
    if (a->maximized_vertically == TRI_TRUE)
         wnck_window_maximize_vertically(window);
    else if (a->maximized_vertically == TRI_FALSE)
      wnck_window_unmaximize_vertically(window);

       /* Handle minimized */
    if (a->minimized == TRI_TRUE)
      wnck_window_minimize(window);
    else if (a->minimized == TRI_FALSE)
#if NEED_TIMESTAMPS
      wnck_window_unminimize(window, gtk_get_current_event_time ());
#else
      wnck_window_unminimize(window);
#endif
    /* Handle full screen */
    if (a->fullscreen != TRI_UNSET) {
        g_print("fullscreen\n");
#if HAVE_FULLSCREEN
      wnck_window_set_fullscreen(window, a->fullscreen);
#else
      g_warning("fullscreen property set but not supported by this libwnck");
#endif
    }
    return TRUE;
  }
  
  public GObject *new(void) {
    return (GObject*)GET_NEW;
  }
}
